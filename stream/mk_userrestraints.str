************************************************************
*                mk_userrestraints.str                     *
************************************************************
* process restraints from csv to "stream/consdef/userrestraints.str"
* the csv file overwrites an existing userrestraints.str file
*

! create a str with information about flexible residues
set istr @proteinunit
set ii 1
label str_loop_punit
   set istr @istr_pep@{ii}
   if @numflex@@{ii} .lt. 1 goto skip_loop_flex
   set jj 1
   label str_loop_flex
      set istr @istr_@flex@@{ii}@@{jj}
      incr jj
      if @{jj} .le. @numflex@@{ii} goto str_loop_flex
   label skip_loop_flex
   incr ii
if @{ii} .le. @proteinunit goto str_loop_punit

! Write @proteinname_1 ... if from psf+crd
if @charmmgui .eq. true then
   ! Read protein topology file
   open read unit 10 card name @ProtTopFile36
   read rtf unit 10 card
   close unit 10
   
   !Read protein parameter file
   open read unit 10 card name @ProtParFile36
   read para unit 10 card flex
   close unit 10

   stream @topDIR/toppar_water_ions.str
   
   bomlev -1
   !read topology
   open read card unit 10 name @topDIR/top_all36_cgenff.rtf
   read rtf card unit 10 append
   close unit 10
   
   !read parameters
   open read card unit 10 name @topDIR/par_all36_cgenff.prm
   read param card unit 10 flex append
   close unit 10
   
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
   stream @strDIR/read_protein.str

   set pun 1
   label writepdbprot
      ! Write protein chains
      open write unit 1 form name @pdbDIR/@proteinname_@pun.pdb
      write coor pdb unit 1 sele segi PEP@pun end
      close unit 1
      
      incr pun
   if @pun .le. @proteinunit goto writepdbprot

   delete atom select all end
endif

! Update readable log file
!open append unit 33 form name enzydock.log
write title unit 33
* Processing definitions in stream/consdef/userrestraints.csv if exists.
* It overwrites stream/consdef/userrestraints.str
*
!close unit 33

! call python script to process the requested noes
set 0 @numligands
set 1 @istr
set 2 @proteinname
close unit 33
system "../scripts/python_wrapper.sh ../scripts/proc_noes.py @0 @1 @2"
open append unit 33 form name enzydock.log

! stop running if there was a problem in atom definitions
bomlev -1
open unit 101 read form name @strDIR/error_restraints.str
if ?iostat .ne. -1 then
   stream @strDIR/error_restraints.str
endif
bomlev @bomblevel

return

