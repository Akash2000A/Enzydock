************************************************************
*                  ENZYDOCK MAIN                           *
************************************************************
* This script controls the EnzyDock program
* Main docking work flow
* Authors:
* Mor Shimshi
* Renana Schwartz
* Susanta Das
* Keren Raz
* Neta Nitoker
* Tamar Ansbacher
* Anil Mhashal
* Dan T Major
* Initial version (EnzyDock v0): 2016-2018
* Mature version (EnzyDock v0.9): July-August 2018
* Advanced version (EnzyDock v1.0): May 2019
* 

! EnzyDock: Proteinâ€“Ligand Docking of Multiple Reactive States along a Reaction Coordinate in Enzymes
! 
! Copyright Â© 2019 Dan T. Major
! 
! Publications using EnzyDock should cite:
!
! Das, S.; Shimshi, M.; Raz, K.; Nitoker, N.; Mhashal, A.; Ansbacher, T.; Major, D. T. 
! EnzyDock: Protein-Ligand Docking of Multiple Reactive States along a Reaction Coordinate in Enzymes. 
! J. Chem. Theory Comput. 2019, 15, 5116-5134.
!
! This program is free software and is intended for academic use only; 
! you can redistribute it and/or modify it for non-profit purposes and under the terms of the
! GNU General Public License as published by the Free Software Foundation;
! either version 2 of the License, or any later version.
! 
! This program is distributed in the hope that it will be useful, 
! but WITHOUT ANY WARRANTY; without even the implied warranty of 
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  
! See the GNU General Public License for more details.
! 
! You should have received a copy of the GNU General Public License along with 
! this program; if not, write to the Free Software Foundation, Inc., 51 Franklin 
! Street, Fifth Floor, Boston, MA  02110-1301, USA. Also add information on 
! how to contact you by electronic and paper mail.
!
! EnzyDock requires the CHARMM program for all functionalities (see: https://academiccharmm.org/)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START SETUP
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
banner
bomlev 0
wrnlev 10
prnlev 5
fast   on
lower
long

! Write-protect all EnzyDock files
system "../scripts/files_lock.sh"

! Update readable log file
open write unit 33 form name enzydock.log
write title unit 33
* Starting EnzyDock run
*
!close unit 33

! Gets job control parameters
stream ../stream/param.str

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! SZ:
! Option to create pdb/str files for ligads from smile strings
! Define prepare_ligands to true (userparam.str)
! Need to place ligands.csv in the pdb directory (see example)
! Will create ligand_{1,2,3,4..}.pdb ligand_{1,2,3,4..}.str on place

if @{prepare_ligands} .eq. true then
    set 0 @cgenff 
    !system "../scripts/prepare.py @0"
    close unit 33
    system "../scripts/python_wrapper.sh ../scripts/prepare.py @0"
    open append unit 33 form name enzydock.log

    bomlev -1
    open unit 101 read form name @strDIR/prepare.str
    if ?iostat .ne. -1 then
        ! Update readable log file
!        open append unit 33 form name enzydock.log
        write title unit 33
* Preparing ligands pdb/str files from smiles string in ./pdb/ligands.csv file
*
!        close unit 33
        stream @strDIR/prepare.str    
    endif
endif

set currligand 1
label no_lone_pair
    ! delete lone pair from toppar
    set 0 @currligand
    close unit 33
    system "../scripts/python_wrapper.sh ../scripts/lp.py @0"
    open append unit 33 form name enzydock.log
    incr currligand
if @currligand .le. @numligands goto no_lone_pair
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Gets the user bomblevel
bomlev @bomblevel

set currligand 1

! Read topology and parameter files
stream @strDIR/top_prm_all.str

goto define_center
label after_define_center

! Reads in protein pdb files, sets up disulfide bridges and 
! writes protein and psf files

! Update readable log file
!open append unit 33 form name enzydock.log
write title unit 33
* Processing protein files (pdb or psf+crd)
*
!close unit 33

if @charmmgui .eq. true then
!   stream @strDIR/read_psf_crd_protein.str
   stream @strDIR/read_protein.str
else
   stream @strDIR/read_write_protein.str
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  SETUP END, START GRID
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Generates the grid using generate_grid.str if the grid has not
! been made. If flexible, delete flexible residues so not
! on grid.

if @flexibledocking .eq. true then
    stream @strDIR/define_flex_offgrid.str
    if @usegrid .eq. true then
       stream @strDIR/write_flex.str
       stream @strDIR/delete_flex.str
    endif
    if @flexibleback .eq. true then
       set kbf_flag false
    endif
endif

if @usegrid .eq. true then 
   ! Update readable log file
!   open append unit 33 form name enzydock.log
   write title unit 33
* Processing grid defenitions (boundaries, flexible residue(s), cofactors, water)
*
!   close unit 33
   ! Calculate grid boundaries
   goto grid_boundaries
   label after_grid_boundaries
   ! Check if flexible residue(s) are within grid boundaries
   if @flexibledocking .eq. true then
      set kbf_flag true ! Switch of restraints in back_flex.str, avoid problems with explicit water
      stream @strDIR/read_flex.str
      stream @strDIR/define_flex_ongrid.str
      goto check_flex_on_grid
      label after_check_flex_on_grid
      stream @strDIR/delete_flex.str
   endif
   ! Check if cofactor(s) are within grid boundaries 
   if @numcofactor .gt. 0 then
      stream @strDIR/generate_cofactors.str
      goto check_cofac_on_grid
      label after_check_cofac_on_grid       
      delete atoms sele cofactors end
   endif
   ! Check if waters are within grid boundaries
   stream @strDIR/read_water.str ! Will look for water pdb file and read if present
   if water .eq. true then
      goto check_water_on_grid
      label after_check_water_on_grid
      ! delete atoms sele segid CWAT end
      ! water inside grid were deleted, outside grid should be included in grid calc
      set water false
   endif
   ! If explicit water requested, set up water file here
!   if isolv .eq. true then
!      if expl .eq. true then
!         stream @strDIR/expl_water.str
!      endif
!   endif
   ! Generate grid
   if @gridone .eq. false then
        ! Update readable log file
!        open append unit 33 form name enzydock.log
        write title unit 33
* Generating grid
*
!        close unit 33
        stream @strDIR/generate_grid.str
    endif

! Deletes the protein, clearing the space for different
! computations
! Now that grid is created, delete protein
if ?nres .gt. 0 then dele atom

! Read in grid here for future use
if @usegrid .eq. true then
   if @gridon .ne. true then
      stream @strDIR/read_grid.str
   endif
endif
! If explicit water requested, set up water file here
if isolv .eq. true then
   if expl .eq. true then
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* Processing addition of explicit water molecules inside the grid
*
!      close unit 33
      stream @strDIR/expl_water.str
   endif
endif

if ?nres .gt. 0 then dele atom

! Reads in the restraints 
!system "sh mkrestraints.sh"
!stream @stdDIR/userrestraints.str

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  END GRID, START MAIN LOOP
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! This is the beginning of the reading in of the ligand for
! the main dynamics and docking loop
grid off
set gridon false

set mcwriteflag true   ! For now always true

! Fingerprint (fp) and concensus docking
if @{fp_docking} .eq. true then
   ! Call to Python code goes here
endif
if @{cons_docking} .eq. true then
   ! Call to Python code goes here
endif

label read_newligand

    ! Reads in a new ligand

    bomlev -1
    open unit 101 read form name @topDIR/ligand_@currligand.str
    if ?iostat .ne. -1 then
       stream @topDIR/ligand_@currligand.str
    else
      ! This is a critical warning
      echo EnzyDock WARNING: Ligand rtf/prm file not found.
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* EnzyDock WARNING: Ligand rtf/prm file not found.
* Terminating EnzyDock run...
*
      close unit 33
      stop
    endif
    bomlev @bomblevel

    ! Update readable log file
!    open append unit 33 form name enzydock.log
    write title unit 33
*    >>> Start processing ligand_@currligand <<<
*
!    close unit 33

    ! Check if ligand contains flexible rings. If not, switch off SAMD
    if smartmc .eq. true then stream @strDIR/no_samd.str

    set iter 1
    set currdock 1
    set mininte 0    
 
    goto write_dat
    label after_write_dat

    if @mkconfs .eq. true then
       ! CHARMM doesn't like this construct of if-inside-if
       !if ?nres .gt. 0 then dele atom
       stream @strDIR/clear_all_restraints.str ! DTM
       if ?nres .gt. 0 then
          dele atom
       endif
       !stream @strDIR/clear_all_restraints.str
       ! Update readable log file
!       open append unit 33 form name enzydock.log
       write title unit 33
* Producing multilpe conformations (in vacuum) of ligand_@currligand,
*    Then clustering them by tosionals
*
!       close unit 33

       stream ligand_mk_conformers.inp @mcwriteflag
       stream niters@currligand.str
    endif

    label dock_iter

       stream @strDIR/clear_all_restraints.str ! DTM
       ! DTM: Check delete command
       if ?nres .gt. 0 then dele atom
       ! RS: clear restraints
       !stream @strDIR/clear_all_restraints.str

       if @mkconfs .ne. true then
          ! Gets a random ligand conformer using HTMC and random rotations
          ! mcwriteflag controls whether to write MC torsional files
          ! Update readable log file
!          open append unit 33 form name enzydock.log
          write title unit 33
* Creating a random conformation of ligand_@currligand
*
!          close unit 33

          stream ligand_simulate.inp @mcwriteflag
       endif
       set mcwriteflag false

       ! Gets a random ligand conformer using HTMC and random rotations
       ! mcwriteflag controls whether to write MC torsional files
       !stream ligand_simulate.inp @mcwriteflag
       !set mcwriteflag false

       ! Docks the random ligand structure using MC and SAMD
       ! Update readable log file
!       open append unit 33 form name enzydock.log
       write title unit 33
* Starting docking of conformer # @iter (out of @maxit) of ligand_@currligand
*
!       close unit 33
       stream dock_ligand.inp
    
       incr iter

    if @iter .le. @maxit goto dock_iter 
    ! Update readable log file
!    open append unit 33 form name enzydock.log
    write title unit 33
* All produced conformers (1-@maxit) of ligand_@currligand were docked
*
!    close unit 33


    ! Call clustering program
    ! The file name parts her (0-3) must not be changed, they correspond to the names
    ! used in dock_ligand.inp
    if @cluster .eq. true then
       set 0 @currligand
       set 1 @runid
       set 2 @maxit
       set 3 @maxmit
       set 4 @clusterwidth
       set 5 @proteinunit
       ! The permutation version of cluster_ligands doesn't account for ligands covalently linked to 
       ! cofactor
       ! Update readable log file
!       open append unit 33 form name enzydock.log
       write title unit 33
* Clustering docked poses of ligand_@currligand with RMSD < @clusterwidth A
*
!       close unit 33
       if @{patch_lig_cofact} .eq. true then
          !system "../scripts/cluster_ligands.py @0 @1 @2 @3 @4 @5"
          close unit 33
          system "../scripts/python_wrapper.sh ../scripts/cluster_ligands.py @0 @1 @2 @3 @4 @5"
          open append unit 33 form name enzydock.log
       else
          !system "../scripts/cluster_ligands_perm.py @0 @1 @2 @3 @4 @5"
          close unit 33
          system "../scripts/python_wrapper.sh ../scripts/cluster_ligands_perm.py @0 @1 @2 @3 @4 @5"
          open append unit 33 form name enzydock.log
       endif
       bomlev -1
       open unit 101 read form name @strDIR/error_cluster.str
       if ?iostat .ne. -1 then
          stream @strDIR/error_cluster.str
       endif
       bomlev @bomblevel
    endif

    stream @strDIR/clear_all_restraints.str ! DTM
    ! DTM: Check delete command
    if ?nres .gt. 0 then dele atom
    ic purge 
    ! RS: clear restraints
    !stream @strDIR/clear_all_restraints.str
    incr currligand
    
    set mcwriteflag true

if @currligand .le. @numligands goto read_newligand

! Do final minimization of ligand in fixed protein without
! consensus restraints
!set usegrid false ! Set grid flag. Note that grid should already be off.

! Clear up all memory and heap used by grid. From here on: all-atom only.
grid clear

if patch_cofact .eq. true set flexibledocking false   ! Switch flexible MM refinement off

stream rmsd_dock.inp

if @cluster .eq. true then

   set currligand 1

   ! Moved inside *_cluster.inp files
   ! Read in number of clusters written by clustering program
   !stream nclusters.str

   !goto write_mm_dat
   !label after_write_mm_dat

!   open append unit 33 form name enzydock.log
   write title unit 33
* Performing MM minimization of ligand_@currligand inside protein:
*
!   close unit 33
   stream mm_cluster.inp

   set currligand 1

   if isolv .eq. true then
      if water .eq. false then
         if gbsw .eq. true then
            !goto write_gbsw_dat
            !label after_write_gbsw_dat
!            open append unit 33 form name enzydock.log
            write title unit 33
* Calculating solvation energy with GBSW for ligand_@currligand:
*
!            close unit 33
            stream gbsw_cluster.inp
            set currligand 1
         endif
         if pbeq .eq. true then
            !goto write_pbeq_dat
            !label after_write_pbeq_dat
!            open append unit 33 form name enzydock.log
            write title unit 33
* Calculating solvation energy with PBEQ for ligand_@currligand:
*
!            close unit 33
            stream pbeq_cluster.inp
            set currligand 1
         endif
      else
         echo EnzyDock WARNING: No implicit solvent calc. done if explicit water molecules are present
         echo Skipping gbsw or pbeq calculations
         ! Update readable log file
!         open append unit 33 form name enzydock.log
         write title unit 33
* EnzyDock WARNING: No implicit solvent calc. done if explicit water molecules are present
*                   Skipping gbsw or pbeq calculations
*
!         close unit 33
      endif
   endif

   ! QM/MM for lowest energy ligands
   if @qmmm .eq. true then
      !goto write_qmmm_dat
      !label after_write_qmmm_dat
      ! Do a QM/MM minimization without consensus restraints
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* Performing QM/MM minimization of ligand_@currligand inside protein:
*
!      close unit 33
      open unit 30 read form name qmmm_cluster.inp
      stream unit 30 !qmmm_cluster.inp
   endif

else

   ! Update readable log file
!   open append unit 33 form name enzydock.log
   write title unit 33
* Performing MM minimization of ligand_@currligand inside protein
*
!   close unit 33
   stream mm.inp

   ! QM/MM for lowest energy ligands
   if @qmmm .eq. true then
   ! Do a QM/MM minimization without consensus restraints
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* Performing QM/MM minimization of ligand_@currligand inside protein
*
!      close unit 33
      open unit 30 read form name qmmm.inp
      stream unit 30 !stream qmmm.inp
   endif

endif

! Creat final report with final scoring
set 0 @numligands
set 1 @runid
set 2 @cluster
close unit 33
system "../scripts/python_wrapper.sh ../scripts/mk_report.py @0 @1 @2"
open append unit 33 form name enzydock.log

!if @covalent .ne. true then stream rmsd_dock.inp   ! Only do for consensus docking
!stream rmsd_dock.inp

stream @strDIR/softlink.str 0

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Update readable log file
!open append unit 33 form name enzydock.log
write title unit 33
* EnzyDock run Finished
*
close unit 33

stop

!**********************************************************
!                  END MAIN ENZYDOCK LOOP
!**********************************************************

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START DEFINE_CENTER
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label define_center

   ! Check if center of active site is set. If not, default
   ! is to take center of current ligand geometry
   set count 0
   if @?bsitex .eq. 0 incr count
   if @?bsitey .eq. 0 incr count
   if @?bsitez .eq. 0 incr count

   if @count .eq. 0 then
      set xcen = @bsitex
      set ycen = @bsitey
      set zcen = @bsitez
   else
      ! Reads in ligand
      bomlev -1
      open unit 101 read form name @topDIR/ligand_@currligand.str
      if ?iostat .ne. -1 then
         stream @topDIR/ligand_@currligand.str
      else
        ! This is a critical warning
        echo EnzyDock WARNING: Ligand rtf/prm file not found.
        ! Update readable log file
!        open append unit 33 form name enzydock.log
        write title unit 33
* EnzyDock WARNING: Ligand rtf/prm file not found.
* Terminating EnzyDock run...
*
        close unit 33
        stop
      endif
      stream @strDIR/read_ligand.str false
      bomlev @bomblevel
      coor stat select segid ligand_@currligand end
      set xcen = ?xave
      set ycen = ?yave
      set zcen = ?zave
      set bsitex = @xcen
      set bsitey = @ycen
      set bsitez = @zcen
      dele atom
   endif

goto after_define_center

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START GRID_BOUNDARIES
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label grid_boundaries

   ! Get extent of grid
!   calc gxmin = @bsitex - @gridsize
!   calc gymin = @bsitey - @gridsize
!   calc gzmin = @bsitez - @gridsize
!   calc gxmax = @bsitex + @gridsize
!   calc gymax = @bsitey + @gridsize
!   calc gzmax = @bsitez + @gridsize

   calc gxmin = @bsitex - @gridsize * 0.5
   calc gymin = @bsitey - @gridsize * 0.5
   calc gzmin = @bsitez - @gridsize * 0.5
   calc gxmax = @bsitex + @gridsize * 0.5
   calc gymax = @bsitey + @gridsize * 0.5
   calc gzmax = @bsitez + @gridsize * 0.5
goto after_grid_boundaries

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START CHECK_FLEX_ON_GRID
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label check_flex_on_grid

   ! Check extent of flexible residues
   coor stat sele flexx end

   set offgrid false
   if ?xmin .lt. @gxmin set offgrid true
   if ?ymin .lt. @gymin set offgrid true
   if ?zmin .lt. @gzmin set offgrid true
   if ?xmax .gt. @gxmax set offgrid true
   if ?ymax .gt. @gymax set offgrid true
   if ?zmax .gt. @gzmax set offgrid true
   if @offgrid .eq. true then
      echo EnzyDock WARNING: FLEXIBLE RESIDUE(S) OFF GRID. INCREASE gridsize AND RERUN.
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* EnzyDock WARNING: FLEXIBLE RESIDUE(S) OFF GRID. INCREASE gridsize AND RERUN.
* Terminating EnzyDock run...
*
      close unit 33
      stop
   else
      echo EnzyDock: Flexible residues(s) within grid. 
   endif

goto after_check_flex_on_grid

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START CHECK_COFAC_ON_GRID
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label check_cofac_on_grid

   ! Check extent of flexible residues
   coor stat sele cofactors end

   set offgrid false
   if ?xmin .lt. @gxmin set offgrid true
   if ?ymin .lt. @gymin set offgrid true
   if ?zmin .lt. @gzmin set offgrid true
   if ?xmax .gt. @gxmax set offgrid true
   if ?ymax .gt. @gymax set offgrid true
   if ?zmax .gt. @gzmax set offgrid true
   if @offgrid .eq. true then
      echo EnzyDock WARNING: COFACTOR(S) OFF GRID. INCREASE gridsize AND RERUN.
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* EnzyDock WARNING: COFACTOR(S) OFF GRID. INCREASE gridsize AND RERUN.
* Terminating EnzyDock run...
*
      close unit 33
      set halfcof true
      !stop
   else
      echo EnzyDock: Cofactor(s) within grid. 
   endif

goto after_check_cofac_on_grid

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START CHECK_WATER_ON_GRID
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label check_water_on_grid

   ! Check extent of flexible residues
   coor stat sele segid CWAT end
 
   ! grid boundaries are defined in label grid_boundaries
   set offgrid false
   if ?xmin .lt. @gxmin set offgrid true
   if ?ymin .lt. @gymin set offgrid true
   if ?zmin .lt. @gzmin set offgrid true
   if ?xmax .gt. @gxmax set offgrid true
   if ?ymax .gt. @gymax set offgrid true
   if ?zmax .gt. @gzmax set offgrid true
   if @offgrid .eq. true then
      echo EnzyDock WARNING: WATER(S) OFF GRID. CHECK WATER POSITIONS.
      ! Update readable log file
!      open append unit 33 form name enzydock.log
      write title unit 33
* EnzyDock WARNING: WATER(S) OFF GRID. CHECK WATER POSITIONS.
*
!      close unit 33
   !   stop
   else
      echo EnzyDock: Water(s) within grid.
   endif

   set tagwtin false
   set tagwout false

   define watin sele segid CWAT .and. ((prop x .gt. @gxmin) -
      .and. (prop x .lt. @gxmax) .and. (prop y .gt. @gymin) -
          .and. (prop y .lt. @gymax) .and. (prop z .gt. @gzmin) -
          .and. (prop z .lt. @gzmax)) show end
   if ?nsel .gt. 0 then
      set tagwtin true
   else
      set tagwout true
      goto no_wat_in
   endif

   define watout sele (segid CWAT .and. .not. .byres. watin) show end
   
   if ?nsel .gt. 0 then
      set tagwout true
      delete atom sele (segid CWAT .and. .not. .byres. watin ) show end
      join CWAT renum
   endif

   rename segid WTIN sele segid CWAT end

! writing psf is dangerous - writes ALL existing atoms
!   open write card unit 1 name @psfDIR/waterin.psf
!   write psf  card unit 1 sele segid WTIN end
!   close unit 1
!
!   open write card unit 1 name @crdDIR/waterin.crd
!   write coor card unit 1 sele segid WTIN end
!   close unit 1

   open write card unit 1  name @pdbDIR/waterin.pdb
   write coor pdb card unit 1 sele segid WTIN end
   delete atom sele segid WTIN end

   stream @strDIR/read_water.str
   define watin sele segid CWAT .and. ((prop x .gt. @gxmin) -
      .and. (prop x .lt. @gxmax) .and. (prop y .gt. @gymin) -
      .and. (prop y .lt. @gymax) .and. (prop z .gt. @gzmin) -
          .and. (prop z .lt. @gzmax)) show end

   if tagwout .eq. false then
      goto no_wat_out
   endif

   if tagwtin .eq. true then
      delete atom sele .byres. watin show end
      join CWAT renum
   endif   

   label no_wat_in

   rename segid WOUT sele segid CWAT end

! writing psf is dangerous - writes ALL existing atoms
!   open write card unit 1 name @psfDIR/waterout.psf
!   write psf  card unit 1 sele segid WOUT end
!   close unit 1
!
!   open write card unit 1 name @crdDIR/waterout.crd
!   write coor card unit 1 sele segid WOUT end
!   close unit 1

   open write card unit 1  name @pdbDIR/waterout.pdb
   write coor pdb card unit 1 sele segid WOUT end
   !delete atom sele segid WOUT end
   ! RS: Don't dele so WOUT will be part of the grid
   
   label no_wat_out

goto after_check_water_on_grid

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                  START WRITE_DAT
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
label write_dat

    ! Starts writing the output summary file
    open write unit 3 form name @resDIR/@currligand_summary_@runid.dat
    write title unit 3
**
*  Summary for ligand number @currligand
*  enert: total potential energy after minimization
*  intere: interaction energy + restraint energy
*  intere0: interaction energy
*  ener0: initial potential energy (soft pot)
*  iter: index for ligand structure
*  microiter: index for rotations of ligand
*  enert        intere      intere0      ener0       iter_microiter
* ==============================================================
* 

goto after_write_dat

